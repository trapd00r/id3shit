#!/usr/bin/perl
use strict;

our $APP       = 'id3shit';
our $VERSION   = 2.2;

use Data::Dumper;
use MP3::Info;
use MP3::Info ':genres';
use File::Copy;
use Getopt::Long;
use Pod::Usage;

pod2usage(verbose=>1) unless(@ARGV);

my $mp3 = MP3::Info->new();

our(@opt_write);
GetOptions(
  'tags=s{1,}'    => sub {shift; read_tags(@_);},
  'rename=s{1,}'  => sub {shift; rename_by_tag(@_);},
  'info=s{1,}'    => sub {shift; show_info(@_);},
  'genres'        => \&list_genres,
  'write=s{1,}'   => \@opt_write,
  'strip=s{1,}'   => sub {shift; remove_tags(@_);},
  'help'          => sub {pod2usage(verbose=>1);},
  'man'           => sub {pod2usage(verbose=>3);},

);

write_tags(@opt_write) if(@opt_write);

sub show_info {
  my @files = @_;
  return unless(@files);

  for my $f(@files) {
    my $info = get_mp3info($f);
    print "Doesnt look like audio to me\n" and exit(1) unless($info);

    printf("%12s: \033[1m%s\033[0m\n", 'File', $f);
    for my $k(sort(keys(%{$info}))) {
      printf("%12s: %s\n",ucfirst(lc($k)),$info->{$k}) unless(ref($info->{$k}));
    }
  }
  exit(0);
}

sub read_tags {
  my $changed_tag = shift; # called from write_tags()?
  my @files = @_;

  if(-f $changed_tag) {
    push(@files, $changed_tag); # ... nope
  }

  my @missing_tags;
  for my $f(@files) {
    my $tag = get_mp3tag($f);
    printf("%7s: \033[1m%s\033[0m\n",'File', $f);

    for(qw(ARTIST ALBUM TITLE GENRE COMMENT)) {
      if(!$tag->{$_}) {
        push(@missing_tags, $_);
        next;
      }
      else {
        if($_ eq uc($changed_tag)) {
          printf("%7s: \033[31;1m%s\033[0m\n", ucfirst(lc($_)),$tag->{$_});
        }
        else {
        printf("%7s: %s\n", ucfirst(lc($_)),$tag->{$_});
      }
    }
  }
    if(scalar(@missing_tags) > 0) {
      printf("\033[1mMissing\033[0m: \033[31m%s\033[0m", lc(join(", ",@missing_tags)));
      printf("\n%7s\n", '---');
    }
  }
  exit(0);
}

sub remove_tags {
  my @files = @_;

  for my $f(@files) {
    my $bytes_removed = remove_mp3tag($f, 'ALL');
    if($bytes_removed < 0) {
      printf("No tags to remove on \033[1m$f\033[0m!\n");
      exit(1);
    }
    else {
      printf("Removed %d bytes from \033[1m$f\033[0m\n", $bytes_removed);
    }
  }
  exit(0);
}

sub rename_by_tag {
  my @nameless = @_;

  for my $f(@nameless) {
    my $info  = get_mp3tag($f);
    if(!$info) {
      printf("\033[1m$f\033[0m: No tags available\n");
      exit(1);
    }

    (my $extension) = $f =~ m;\.(.+)$;;
    if(!$extension) {
      $extension = 'mp3';
    }

    my ($artist,$album,$title,$year,$genre,$comment,$tracknum)
      = ($info->{ARTIST}, $info->{ALBUM}, $info->{TITLE}, $info->{YEAR},
        $info->{GENRE}, $info->{COMMENT}, $info->{TRACKNUM});

    if(!defined($artist)) {
      print("$f: Missing artist tag\n");
    }
    for(($artist,$album,$title,)) {
      s;\s;_;g;
      s;\(;-;g;
      s;\);-;g;
      s;/;-;g;
    }
    my $newfile = sprintf("%s_-_%s-%s",$artist, $album, $title) . ".$extension";
    print "\033[1mOld\033[0m: \033[31m $f\033[0m\n";
    print "\033[1mNew\033[0m: \033[34m $newfile\033[0m\n";

    print "Accept [y/N]: ";
    chomp(my $confirmation = <STDIN>);

    exit(0) if(lc($confirmation) ne 'y');
    print "---\n";

    if(move("$f", $newfile)) {
      printf("%s => \033[1m%s\033[0m\n", $f, $newfile);
    }
    else {
      print("[$f => $newfile]: \033[1m$!\033[0m");
    }
  }
  exit(0);
}

sub write_tags {
  my $tag_name = shift;
  my $tag_data = shift;
  my @files = @_;
  for my $f(@files) {
    my $tags = get_mp3tag($f);
    $tags->{uc($tag_name)} = ucfirst($tag_data); # ARTIST Laleh
    if(set_mp3tag($f, $tags)) {
      read_tags($tag_name,$f);
    }
    else {
      print($!);
    }
  }
  exit(0);
}

sub list_genres {
  for my $genre(sort(keys(%mp3_genres))) {
    printf("%2d: %s\n", $mp3_genres{$genre},$genre);
  }
  exit(0);
}

=pod

=head1 NAME

  id3shit - commandline based id3 editor

=head1 SYNOPSIS

  id3shit [OPTION]... FILES

=head1 DESCRIPTION

B<id3shit> is a commandline based id3 editor that sucks a little bit less then
the alternatives.

It can read, write and strip tags, rename files based on the metadata and that's
about it.

=head1 OPTIONS

  -t,   --tags    show tags for FILEs
  -i,   --info    show audio info for FILEs
  -s,   --strip   strip all tags from FILEs
  -w,   --write   write TAG DATA to FILEs ( -w artist Laleh )
  -g,   --genres  list all available genres
  -r,   --rename  rename FILEs based on ID3 data
  -h,   --help    display a short help and exit
  -m,   --man     display the manual

=head2 'write' syntax

    write will take two arguments: the tag field to change, and the data to place
    there:

    id3shit -w artist Laleh

    Legal fields are ARTIST, ALBUM, TITLE, YEAR, COMMENT, GENRE and TRACKNUM

=head1 AUTHOR

Written by Magnus Woldrich.

=head1 REPORTING BUGS

Report bugs to trapd00r@trapd00r.se

id3shit homepage: http://github.com/trapd00r/id3shit

=head1 COPYRIGHT

Copyright (C) 2010 Magnus Woldrich.

License: GPLv2

=head1 HISTORY

The original id3shit started out as a way for me to learn some Perl, being
teached by zibri (also known as The Perl Dude).

The original id3shit project was founded because the authors were trying to
strip tags from some mp3 files (a simple task, eh?) and none of the available
commandline based editors worked.

Zibri and trapd00r started hacking (this was my chance to learn some Perl from
The Perl Dude) and soon we had a working version.

Well, it was working, but it really sucked. Just like all the others.

As of today (2010-08-17), I once again felt the need to edit some id3 tags. I
remembered id3shit and it worked just fine, but I had to check the source code,
and man, it was fugly.

All programmers are familiar with that cycle though, and that's a good thing. :)
=cut
